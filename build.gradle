plugins {
    id 'java'
    id 'application'
    id "io.freefair.lombok" version "6.6.1"
    // https://github.com/qoomon/gradle-git-versioning-plugin
    id 'me.qoomon.git-versioning' version '6.3.8'
    id "io.github.itzg.github-releaser" version "0.1.1"
}

group 'io.github.itzg'
version = '0.0.1-SNAPSHOT'
gitVersioning.apply {
    refs {
        tag('.+') {
            version = '${ref}'
        }
        branch('main') {
            version = '0.0.0-${describe.tag}-SNAPSHOT'
        }
        branch('.+') {
            version = '0.0.0-${ref}-SNAPSHOT'
        }
    }

    // optional fallback configuration in case of no matching ref configuration
    rev {
        version = '${commit}'
    }

}

repositories {
    mavenCentral()
}

application {
    mainClass = 'app.FileTreeAsJson'
}

dependencies {
    implementation 'ch.qos.logback:logback-classic:1.4.5'
    implementation 'info.picocli:picocli:4.7.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

test {
    useJUnitPlatform()
}